(ns murakumo.ci.release-bundle
  "Host-side construction of deployable bundles from verified CI artifacts."
  (:require [clojure.edn :as edn]
            [clojure.string :as str]
            [kotoba-git.object :as object]
            [kotoba-git.repo :as repo]
            [murakumo.canonical :as canonical]
            [murakumo.cd.bundle :as bundle]
            [murakumo.ci.attest :as attest]))

(def bundle-path "release.bundle.edn")

(defn- verify-bytes [expected-cid bytes]
  (= expected-cid (second (object/write-blob (repo/empty-repo) bytes))))

(defn build!
  "Build and persist a release bundle from receipt artifacts already verified
   by `murakumo.ci.host`. `manifest-path` identifies the resolved app manifest.
   Returns the receipt with a typed release-bundle artifact attached."
  [receipt manifest-path fetch-bytes store-artifact!]
  (let [artifacts (attest/artifact-manifest receipt)
        manifest-artifact (first (filter #(= manifest-path (:path %)) artifacts))]
    (when-not manifest-artifact
      (throw (ex-info "murakumo-ci: resolved app manifest artifact missing"
                      {:reason :release-manifest-missing :path manifest-path})))
    (let [manifest-bytes (fetch-bytes (:cid manifest-artifact))]
      (when-not (and manifest-bytes (verify-bytes (:cid manifest-artifact) manifest-bytes))
        (throw (ex-info "murakumo-ci: resolved app manifest unavailable or corrupt"
                        {:reason :release-manifest-corrupt})))
      (let [manifest (String. ^bytes manifest-bytes java.nio.charset.StandardCharsets/UTF_8)
            parsed (try (edn/read-string manifest) (catch Exception _ nil))
            manifest-cids (set (keep #(or (:cid %) (:kotoba.component/cid %))
                                     (:kotoba.app/components parsed)))
            components (->> artifacts
                            (remove #(= manifest-path (:path %)))
                            (filter #(contains? manifest-cids (:cid %)))
                            (mapv #(select-keys % [:path :cid])))
            revision (or (get-in receipt [:receipt/source :source/revision])
                         (get-in receipt [:receipt/source :source/git-revision]))]
        (when-not (= manifest-cids (set (map :cid components)))
          (throw (ex-info "murakumo-ci: manifest component is not a verified artifact"
                          {:reason :release-component-missing
                           :manifest-cids manifest-cids
                           :artifact-cids (set (map :cid components))})))
        (let [document (bundle/create {:revision revision :manifest manifest
                                       :components components})
              bytes (canonical/encode-bytes document)
              bundle-cid (bundle/cid document)
              descriptor {:path bundle-path :cid bundle-cid :size (alength ^bytes bytes)
                          :type :murakumo/release-bundle}]
          (store-artifact! bundle-cid bytes)
          (assoc receipt :receipt/release-bundle descriptor))))))
