(ns murakumo.ci.pipeline
  "Loader and validator for the repository-owned .murakumo/pipeline.edn."
  (:require [ci.execute :as execute]
            [ci.validate :as validate]
            [clojure.edn :as edn]
            [clojure.java.io :as io]
            [murakumo.canonical :as canonical]
            [murakumo.ci.sandbox :as sandbox]
            [murakumo.identity :as identity]))

(def default-path ".murakumo/pipeline.edn")

(defn digest [pipeline]
  (identity/graph-cid (canonical/string pipeline)))

(defn- actions-validation-view
  "Older ci-clj releases know :ci/run but not :ci/argv. Present argv as a
   run action only to the structural validator; execution keeps the argv."
  [pipeline]
  (update pipeline :ci/jobs
          (fn [jobs]
            (into {} (for [[id job] jobs]
                       [id (update job :ci/steps
                                   (fn [steps]
                                     (mapv #(if (contains? % :ci/argv)
                                              (-> % (dissoc :ci/argv)
                                                  (assoc :ci/run "<argv>"))
                                              %)
                                           steps)))])))))

(defn problems [pipeline]
  (let [structural (validate/problems (actions-validation-view pipeline))
        sandbox-problems
        (vec (for [[job-id job] (:ci/jobs pipeline)
                   [index step] (map-indexed vector (:ci/steps job))
                   :let [reason (sandbox/validate-step step)]
                   :when reason]
               {:ci/severity :error :ci/code :step/invalid-sandbox
                :ci/id job-id :ci/step index :ci/reason reason}))]
    (into structural sandbox-problems)))

(defn valid? [pipeline]
  (not-any? #(= :error (:ci/severity %)) (problems pipeline)))

(defn load-pipeline
  ([root] (load-pipeline root default-path))
  ([root relative-path]
   (let [file (io/file root relative-path)]
     (when-not (.isFile file)
       (throw (ex-info "murakumo-ci: pipeline file not found"
                       {:reason :pipeline-not-found :path (.getPath file)})))
     (let [pipeline (edn/read-string {:readers {} :default (fn [tag _]
                                                              (throw (ex-info "tagged literal rejected"
                                                                              {:tag tag})))}
                                     (slurp file))
           ps (problems pipeline)]
       (when (some #(= :error (:ci/severity %)) ps)
         (throw (ex-info "murakumo-ci: invalid pipeline"
                         {:reason :invalid-pipeline :problems ps})))
       {:pipeline pipeline :pipeline-digest (digest pipeline)
        :waves (execute/plan pipeline) :path (.getCanonicalPath file)}))))
