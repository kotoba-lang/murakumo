(ns murakumo.ci.runner-daemon
  "Long-running Murakumo QUIC CI runner entrypoint."
  (:require [clojure.edn :as edn]
            [clojure.string :as str]
            [ed25519.core :as ed]
            [murakumo.canonical :as canonical]
            [murakumo.ci.worker :as worker]
            [murakumo.identity :as identity]))

(def version 1)

(defn valid-config? [config]
  (let [broker (:ci.runner/broker-request config)
        paths (:ci.runner/paths config)]
    (and (= version (:ci.runner/version config))
         (every? #(and (string? %) (not (str/blank? %)))
                 [(:ci.runner/rid config) (:ci.runner/seed-env config)
                  (:ci.runner/environment-digest config)
                  (:workspace-root paths) (:artifact-root paths)])
         (set? (:ci.runner/capabilities config))
         (map? (:ci.runner/source-remotes config))
         (every? string? (vals (:ci.runner/source-remotes config)))
         (= "murakumo.overlay.adapter-request" (:type broker))
         (= :quic (:transport broker))
         (pos-int? (get-in broker [:connect :port])))))

(defn load-config [path]
  (let [config (edn/read-string (slurp path))]
    (when-not (valid-config? config)
      (throw (ex-info "murakumo-ci: invalid runner config"
                      {:reason :invalid-runner-config})))
    config))

(defn build
  ([config] (build config #(System/getenv %)))
  ([config getenv]
   (when-not (valid-config? config)
     (throw (ex-info "murakumo-ci: invalid runner config"
                     {:reason :invalid-runner-config})))
   (let [seed-hex (getenv (:ci.runner/seed-env config))
         seed (try (some-> seed-hex ed/unhex) (catch Throwable _ nil))]
     (when-not (= 32 (some-> seed alength))
       (throw (ex-info "murakumo-ci: runner Ed25519 seed unavailable"
                       {:reason :missing-runner-seed})))
     (let [runner-id (ed/did-key-from-seed seed)
           paths (:ci.runner/paths config)
           rpc (worker/rpc-client
                {:broker-request (:ci.runner/broker-request config)
                 :timeout-ms (or (:ci.runner/rpc-timeout-ms config) 10000)})]
       {:rpc rpc
        :runner {:runner/id runner-id
                 :runner/capabilities (:ci.runner/capabilities config)
                 :runner/environment-digest (:ci.runner/environment-digest config)}
        :runner-id runner-id :signer-seed seed :rid (:ci.runner/rid config)
        :workspace-root (:workspace-root paths) :artifact-root (:artifact-root paths)
        :source-remotes (:ci.runner/source-remotes config)
        :runtime (or (:ci.runner/runtime config) "podman")
        :release-manifest-path (:ci.runner/release-manifest-path config)
        :mirror-artifacts? true
        :heartbeat-ms (or (:ci.runner/heartbeat-ms config) 20000)}))))

(defn run-loop! [config]
  (let [opts (build config)
        poll-ms (or (:ci.runner/poll-ms config) 5000)]
    (loop []
      (let [result (worker/poll-once! opts)]
        (when (= :completed (:status result))
          (println (pr-str {:ci.runner/status :completed
                            :ci.runner/result
                            (get-in result [:result :execution :result])})))
        (when (= :idle (:status result)) (Thread/sleep poll-ms))
        (recur)))))

(defn -main [& [config-path]]
  (when (str/blank? config-path)
    (throw (ex-info "usage: clojure -M:ci-runner <config.edn>"
                    {:reason :missing-config-path})))
  (run-loop! (load-config config-path)))
