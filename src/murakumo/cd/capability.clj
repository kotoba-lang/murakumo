(ns murakumo.cd.capability
  "Short-lived, environment- and artifact-scoped deployment capabilities."
  (:require [kotoba-git.object :as object]
            [kotoba-git.repo :as repo]
            [kotoba-rad.sigref :as sigref]
            [murakumo.canonical :as canonical]))

(defn document-cid [document]
  (second (object/write-blob (repo/empty-repo) (canonical/encode-bytes document))))

(defn capability-ref [capability-cid]
  (str "refs/cd/capabilities/" capability-cid))

(defn issue
  "Issue and sign a capability after the caller has established canonical CI
   verdict. Time values are epoch seconds and supplied by the host."
  [issuer-seed rid {:keys [verdict-cid artifact-cid previous-artifact-cid
                           environment revision previous-revision now ttl nonce]}]
  (when-not (and verdict-cid artifact-cid (string? environment)
                 (string? previous-artifact-cid) (string? revision)
                 (string? previous-revision)
                 (pos-int? ttl) (seq nonce))
    (throw (ex-info "murakumo-cd: invalid capability request"
                    {:reason :invalid-capability-request})))
  (let [document {:cd.capability/version 1
                  :cd.capability/verdict-cid verdict-cid
                  :cd.capability/artifact-cid artifact-cid
                  :cd.capability/previous-artifact-cid previous-artifact-cid
                  :cd.capability/environment environment
                  :cd.capability/revision revision
                  :cd.capability/previous-revision previous-revision
                  :cd.capability/not-before now
                  :cd.capability/expires-at (+ now ttl)
                  :cd.capability/nonce nonce}
        cid (document-cid document)
        sr (sigref/sign issuer-seed rid (capability-ref cid) cid now)]
    {:capability document :capability-cid cid :sigref sr}))

(defn verify
  [{:keys [rid issuers now environment artifact-cid previous-artifact-cid
           verdict-cid revision previous-revision]}
   {:keys [capability capability-cid sigref]}]
  (and (= capability-cid (document-cid capability))
       (sigref/valid? sigref)
       (= rid (get sigref "rid"))
       (= capability-cid (get sigref "commit"))
       (= (capability-ref capability-cid) (get sigref "ref"))
       (contains? issuers (get sigref "signer"))
       (<= (:cd.capability/not-before capability) now)
       (< now (:cd.capability/expires-at capability))
       (= environment (:cd.capability/environment capability))
       (= revision (:cd.capability/revision capability))
       (= previous-revision (:cd.capability/previous-revision capability))
       (= artifact-cid (:cd.capability/artifact-cid capability))
       (= previous-artifact-cid (:cd.capability/previous-artifact-cid capability))
       (= verdict-cid (:cd.capability/verdict-cid capability))))

(defn deployment-receipt
  "Content-address a deployment outcome; signing can reuse sigref over the
   returned CID under refs/cd/deployments/<capability-cid>."
  [{:keys [capability-cid artifact-cid environment status revision]}]
  (let [document {:cd.receipt/version 1 :cd.receipt/capability-cid capability-cid
                  :cd.receipt/artifact-cid artifact-cid
                  :cd.receipt/environment environment :cd.receipt/status status
                  :cd.receipt/revision revision}
        cid (document-cid document)]
    {:receipt document :receipt-cid cid
     :ref (str "refs/cd/deployments/" capability-cid)}))
