(ns murakumo.cd.service
  "Composition root for a fleet node's capability-gated CD endpoint."
  (:require [murakumo.artifact-remote :as artifact-remote]
            [murakumo.cd.node-ops :as node-ops]
            [murakumo.cd.remote :as remote]))

(defn create-handler
  "Combine node-local trust policy and argv operation configuration into the
   handler served over Murakumo QUIC.

   `trust-opts`: node-id, rid, issuers, clock-fn.
   `operation-opts`: state-file, three local argv builders, optional exec-fn,
   timeout-ms and clock-fn."
  [trust-opts operation-opts]
  (remote/handler (merge trust-opts (node-ops/operation-set operation-opts))))

(defn create-node-handler
  "Multiplex artifact replication and deployment operations on one QUIC RPC
   listener. Both handlers independently revalidate the signed capability."
  [trust-opts operation-opts artifact-opts]
  (let [cd-handler (create-handler trust-opts operation-opts)
        artifact-handler (artifact-remote/handler (merge trust-opts artifact-opts))]
    (fn [payload]
      (case (:murakumo.artifact/type payload)
        :request (artifact-handler payload)
        (cd-handler payload)))))

(defn create-node-handler-with-operations
  [trust-opts operation-fns artifact-opts]
  (let [cd-handler (remote/handler (merge trust-opts operation-fns))
        artifact-handler (artifact-remote/handler (merge trust-opts artifact-opts))]
    (fn [payload]
      (if (= :request (:murakumo.artifact/type payload))
        (artifact-handler payload)
        (cd-handler payload)))))

(defn serve!
  ([overlay-request trust-opts operation-opts]
   ((requiring-resolve 'murakumo.overlay.quic-driver/serve-rpc!)
    overlay-request (create-handler trust-opts operation-opts)))
  ([overlay-request trust-opts operation-opts artifact-opts]
   ((requiring-resolve 'murakumo.overlay.quic-driver/serve-rpc!)
    overlay-request (create-node-handler trust-opts operation-opts artifact-opts))))
