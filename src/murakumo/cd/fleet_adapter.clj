(ns murakumo.cd.fleet-adapter
  "Controller-side adapter from cd.executor callbacks to overlay QUIC RPC."
  (:require [murakumo.cd.protocol :as protocol]))

(def default-timeout-ms 10000)

(defn create-overlay-adapter
  [{:keys [node-lookup session issued-capability timeout-ms request-fn stage-fn]}]
  (let [timeout-ms (or timeout-ms default-timeout-ms)
        document (:capability issued-capability)
        artifact (:cd.capability/artifact-cid document)
        previous-artifact (:cd.capability/previous-artifact-cid document)
        environment (:cd.capability/environment document)
        verdict (:cd.capability/verdict-cid document)
        invoke (fn [action {:keys [node revision artifact-cid]}]
                 (if-let [target (node-lookup node)]
                   (try
                     (let [operation-artifact (or artifact-cid
                                                  (if (= action :rollback)
                                                    previous-artifact artifact))
                           staged (if (and stage-fn (#{:deploy :rollback} action))
                                    (stage-fn {:node node :artifact-cid operation-artifact
                                               :revision revision :action action})
                                    {:ok? true})
                           overlay-request {:type "murakumo.overlay.adapter-request"
                                            :version 1
                                            :transport :quic
                                            :session session
                                            :connect target}
                           payload (protocol/request action node issued-capability
                                                     operation-artifact environment verdict revision)
                           transport (when (:ok? staged)
                                       ((or request-fn
                                            (requiring-resolve 'murakumo.overlay.quic-driver/request!))
                                        overlay-request payload timeout-ms))]
                       (if-not (:ok? staged)
                         (assoc staged :reason :artifact-staging-failed
                                :staging-reason (:reason staged))
                         (let [{:keys [ok? response error]} transport]
                           (if (and ok? (protocol/valid-response? response action node))
                             (:murakumo.cd/result response)
                             {:ok? false :reason :transport-failed
                              :error (or error :invalid-response)}))))
                     (catch Throwable t
                       {:ok? false :reason :transport-failed
                        :error (.getMessage t)}))
                   {:ok? false :reason :node-not-found}))]
    {:deploy-fn #(invoke :deploy %)
     :health-fn #(invoke :health %)
     :rollback-fn #(invoke :rollback %)}))
