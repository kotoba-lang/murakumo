(ns murakumo.cd.node-daemon
  "Production composition and entrypoint for one Murakumo CD fleet node."
  (:require [clojure.edn :as edn]
            [clojure.java.io :as io]
            [clojure.string :as str]
            [murakumo.artifact-store :as artifact-store]
            [murakumo.cd.kotoba-ops :as kotoba-ops]
            [murakumo.cd.service :as service]))

(def version 1)

(defn valid-config? [config]
  (let [paths (:cd.node/paths config)
        kotoba (:cd.node/kotoba config)
        overlay (:cd.node/overlay-request config)]
    (and (= version (:cd.node/version config))
         (every? #(and (string? %) (not (str/blank? %)))
                 [(:cd.node/node-id config) (:cd.node/rid config)
                  (:artifact-root paths) (:transfer-temp paths)
                  (:releases-root paths) (:active-state paths)
                  (:binary kotoba) (:token-env kotoba) (:url kotoba)
                  (:wit-dir kotoba) (:health-url kotoba)])
         (set? (:cd.node/issuers config))
         (seq (:cd.node/issuers config))
         (every? #(and (string? %) (str/starts-with? % "did:key:"))
                 (:cd.node/issuers config))
         (not (contains? kotoba :token))
         (= "murakumo.overlay.adapter-request" (:type overlay))
         (= 1 (:version overlay))
         (= :quic (:transport overlay))
         (string? (get-in overlay [:connect :host]))
         (pos-int? (get-in overlay [:connect :port])))))

(defn load-config [path]
  (let [config (edn/read-string (slurp (io/file path)))]
    (when-not (valid-config? config)
      (throw (ex-info "murakumo-cd: invalid node daemon config"
                      {:reason :invalid-node-config :path path})))
    config))

(defn build
  ([config] (build config #(System/getenv %)))
  ([config getenv]
   (when-not (valid-config? config)
     (throw (ex-info "murakumo-cd: invalid node daemon config"
                     {:reason :invalid-node-config})))
   (let [paths (:cd.node/paths config)
         kotoba (:cd.node/kotoba config)
         token (getenv (:token-env kotoba))]
     (when (str/blank? token)
       (throw (ex-info "murakumo-cd: Kotoba operator token is unavailable"
                       {:reason :missing-kotoba-token :env (:token-env kotoba)})))
     (let [cas (artifact-store/adapter (:artifact-root paths))
           trust {:node-id (:cd.node/node-id config)
                  :rid (:cd.node/rid config)
                  :issuers (:cd.node/issuers config)
                  :clock-fn #(quot (System/currentTimeMillis) 1000)}
           operations (kotoba-ops/operation-set
                       {:fetch-bytes (:get cas)
                        :releases-root (:releases-root paths)
                        :state-file (:active-state paths)
                        :kotoba (:binary kotoba) :token token :url (:url kotoba)
                        :wit-dir (:wit-dir kotoba) :health-url (:health-url kotoba)
                        :timeout-ms (or (:timeout-ms kotoba) 60000)})
           artifact {:temp-dir (:transfer-temp paths)
                     :put! (:put! cas) :get-bytes (:get cas)
                     :max-object-bytes (or (:cd.node/max-object-bytes config)
                                           (* 512 1024 1024))}]
       {:overlay-request (:cd.node/overlay-request config)
        :handler (service/create-node-handler-with-operations
                  trust operations artifact)}))))

(defn serve! [config]
  (let [{:keys [overlay-request handler]} (build config)]
    ((requiring-resolve 'murakumo.overlay.quic-driver/serve-rpc!)
     overlay-request handler)))

(defn -main [& [config-path]]
  (when (str/blank? config-path)
    (throw (ex-info "usage: clojure -M:cd-node <node-config.edn>"
                    {:reason :missing-config-path})))
  (serve! (load-config config-path)))
