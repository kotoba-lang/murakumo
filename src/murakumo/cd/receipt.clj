(ns murakumo.cd.receipt
  "Persist and sign terminal rollout receipts."
  (:require [kotoba-git.object :as object]
            [kotoba-git.refs :as refs]
            [kotoba-rad.sigref :as sigref]
            [murakumo.canonical :as canonical]))

(defn attest
  [db rid executor-seed rollout-result ts]
  (let [document (:receipt rollout-result)
        ref-name (:ref rollout-result)]
    (when-not (and document ref-name (:receipt-cid rollout-result))
      (throw (ex-info "murakumo-cd: incomplete rollout receipt"
                      {:reason :invalid-rollout-receipt})))
    (let [bytes (canonical/encode-bytes document)
          [db blob-cid] (object/write-blob db bytes)
          [db tree-cid] (object/write-tree db [{:name "deployment-receipt.edn"
                                                :cid blob-cid :kind :blob}])
          sr (sigref/sign executor-seed rid ref-name blob-cid ts)
          signer (get sr "signer")
          [db commit-cid] (object/write-commit
                           db {:tree tree-cid :parents [] :author signer
                               :message (str "Deployment receipt " ref-name) :ts 0})]
      {:db (refs/set-ref db rid ref-name commit-cid)
       :receipt-blob-cid blob-cid :receipt-commit-cid commit-cid
       :sigref sr :ref ref-name})))

(defn valid?
  [{:keys [rid executors]} attestation]
  (let [sr (:sigref attestation)]
    (and (sigref/valid? sr)
         (= rid (get sr "rid"))
         (= (:ref attestation) (get sr "ref"))
         (= (:receipt-blob-cid attestation) (get sr "commit"))
         (contains? executors (get sr "signer")))))
