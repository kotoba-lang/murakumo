(ns murakumo.cd.remote
  "Fleet-node endpoint for deployment RPCs. Authorization is deliberately
   reconstructed from local trust configuration, never accepted from the
   controller."
  (:require [murakumo.cd.capability :as capability]
            [murakumo.cd.protocol :as protocol]))

(defn- denied [reason]
  {:ok? false :reason reason})

(defn handler
  [{:keys [node-id rid issuers clock-fn deploy-fn health-fn rollback-fn]}]
  (fn [request]
    (let [action (:murakumo.cd/action request)
          respond #(protocol/response action node-id %)]
      (try
        (if-not (protocol/valid-request? request)
          (respond (denied :invalid-request))
          (if-not (= node-id (:murakumo.cd/node request))
            (respond (denied :wrong-node))
            (let [issued (:murakumo.cd/issued-capability request)
                  document (:capability issued)
                  revision (:murakumo.cd/revision request)
                  expected-revision (if (= action :rollback)
                                      (:cd.capability/previous-revision document)
                                      (:cd.capability/revision document))
                  expected-artifact (if (= action :rollback)
                                      (:cd.capability/previous-artifact-cid document)
                                      (:cd.capability/artifact-cid document))
                  policy {:rid rid
                          :issuers issuers
                          :now (clock-fn)
                          :environment (:murakumo.cd/environment request)
                          :artifact-cid (:cd.capability/artifact-cid document)
                          :previous-artifact-cid (:cd.capability/previous-artifact-cid document)
                          :verdict-cid (:murakumo.cd/verdict-cid request)
                          :revision (:cd.capability/revision document)
                          :previous-revision (:cd.capability/previous-revision document)}]
              (cond
                (not= revision expected-revision)
                (respond (denied :revision-out-of-scope))

                (not= (:murakumo.cd/artifact-cid request) expected-artifact)
                (respond (denied :artifact-out-of-scope))

                (not (capability/verify policy issued))
                (respond (denied :unauthorized))

                :else
                (let [operation (case action
                                  :deploy deploy-fn
                                  :health health-fn
                                  :rollback rollback-fn)]
                  (respond
                   (operation {:node node-id
                               :artifact-cid (:murakumo.cd/artifact-cid request)
                               :environment (:murakumo.cd/environment request)
                               :revision revision})))))))
        (catch Throwable t
          (respond {:ok? false :reason :operation-failed
                    :message (.getMessage t)}))))))

(defn serve!
  "Serve a CD handler through the native overlay QUIC RPC driver."
  [overlay-request opts]
  ((requiring-resolve 'murakumo.overlay.quic-driver/serve-rpc!)
   overlay-request (handler opts)))
