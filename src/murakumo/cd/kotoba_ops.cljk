(ns murakumo.cd.kotoba-ops
  "Concrete node-local activation of verified release bundles through Kotoba."
  (:require [clojure.java.io :as io]
            [murakumo.cd.bundle :as bundle]
            [murakumo.cd.node-ops :as node-ops]))

(defn block-put-argv [kotoba url token file]
  [kotoba "--url" url "--token" token "block" "put" "--file" file])

(defn deploy-argv [kotoba manifest-path wit-dir url]
  [kotoba "app" "deploy" manifest-path "--wit-dir" wit-dir
   "--publish" "--url" url])

(defn health-argv [curl health-url]
  [curl "--fail" "--silent" "--show-error" "--max-time" "5" health-url])

(defn- activation-plan
  [{:keys [fetch-bytes releases-root kotoba token url wit-dir]} request]
  (let [document (bundle/load-bundle fetch-bytes (:artifact-cid request))]
    (when-not (= (:revision request) (:cd.bundle/revision document))
      (throw (ex-info "murakumo-cd: release revision does not match bundle"
                      {:reason :bundle-revision-mismatch})))
    (let [{:keys [release-dir manifest-path]}
          (bundle/materialize! fetch-bytes releases-root (:artifact-cid request) document)
          component-files (mapv #(str (io/file release-dir (:path %)))
                                (:cd.bundle/components document))]
      (conj (mapv #(block-put-argv kotoba url token %) component-files)
            (deploy-argv kotoba manifest-path wit-dir url)))))

(defn operation-set
  "Build remote handler callbacks for the actual Kotoba block-put + app-deploy
   contract. Bundle resolution and CID verification happen before any command."
  [{:keys [curl health-url] :or {curl "/usr/bin/curl"} :as opts}]
  (node-ops/operation-set
   (merge opts
          {:deploy-argv-fn #(activation-plan opts %)
           :rollback-argv-fn #(activation-plan opts %)
           :health-argv-fn (fn [_] (health-argv curl health-url))})))
