(ns murakumo.infer.profile
  "Measured llama.cpp serving profiles for the resident Murakumo heads."
  (:require [clojure.pprint :as pprint]
            [kotodama.inference.runtime :as runtime]))

(def nodes
  {"b70" {:adapter "Intel Arc Pro B70 8086:e223"
          :intent :latency :ctx 32768}
   "gad" {:adapter "Radeon 8060S gfx1151"
          :intent :throughput :ctx 524288}
   "xavier" {:adapter "Jetson AGX Xavier tegra194"
              :intent :fallback :ctx 8192}})

(defn serving-profile [node]
  (if-let [{:keys [adapter intent ctx]} (get nodes node)]
    (assoc (runtime/performance adapter intent)
           :murakumo/node node
           :murakumo/model "qwen3.8-27b"
           :murakumo/ctx ctx)
    (throw (ex-info "unknown optimized inference node"
                    {:node node :known (sort (keys nodes))}))))

(defn llama-args [node]
  (let [p (serving-profile node)
        speculative (:kotodama/speculative p)]
    (vec
     (concat ["--ctx-size" (str (:murakumo/ctx p))
              "--parallel" (str (:kotodama/max-running p))]
             (when (:kotodama/flash-attention? p)
               ["--flash-attn" "on"])
             (when-let [n (:kotodama/batch-size p)]
               ["--batch-size" (str n)])
             (when-let [n (:kotodama/ubatch-size p)]
               ["--ubatch-size" (str n)])
             (case (:type speculative)
               :mtp ["--spec-type" "draft-mtp"
                     "--spec-draft-n-max" (str (:draft-token-count speculative))]
               :ngram-cache ["--spec-type" "ngram-cache,ngram-simple"]
               :none []
               [])))))

(defn -main [& [node]]
  (if node
    (pprint/pprint {:profile (serving-profile node)
                    :llama-args (llama-args node)})
    (doseq [name (sort (keys nodes))]
      (pprint/pprint {:profile (serving-profile name)
                      :llama-args (llama-args name)}))))
