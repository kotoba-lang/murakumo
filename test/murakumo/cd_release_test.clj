(ns murakumo.cd-release-test
  (:require [clojure.test :refer [deftest is]]
            [ed25519.core :as ed]
            [kotoba-rad.sigref :as sigref]
            [murakumo.cd.capability :as capability]
            [murakumo.cd.release :as release]
            [murakumo.ci.attest :as attest]))

(def runner-seeds [(byte-array (repeat 32 (byte 1)))
                   (byte-array (repeat 32 (byte 2)))])
(def runner-dids (set (map ed/did-key-from-seed runner-seeds)))
(def issuer-seed (byte-array (repeat 32 (byte 9))))
(def verdict-cid "bafy-verdict")
(def verdict {:verdict/status :passed
              :verdict/artifacts [{:path "release.bundle.edn" :cid "bafy-artifact"
                                   :type :murakumo/release-bundle}]})
(def result-ref (attest/result-ref "run-1"))
(def votes [(sigref/sign (first runner-seeds) "rid-ci" result-ref verdict-cid 1)
            (sigref/sign (second runner-seeds) "rid-ci" result-ref verdict-cid 2)])
(def policy {:rid "rid-ci" :run-id "run-1" :delegates runner-dids :threshold 2})

(deftest deployment-requires-passed-canonical-artifact
  (let [pending (release/finalize-ci policy [(first votes)] {verdict-cid verdict})
        final (release/finalize-ci policy votes {verdict-cid verdict})]
    (is (= :pending (:state pending)))
    (is (= :canonical (:state final)))
    (is (= :running (:result (release/github-status pending "o/r" "sha" "run-1" "url"))))
    (is (thrown? clojure.lang.ExceptionInfo
                 (release/issue-deployment issuer-seed "rid-ci" pending
                                           {:artifact-cid "bafy-artifact" :environment "prod"
                                            :previous-artifact-cid "bafy-previous"
                                            :revision "v2" :previous-revision "v1"
                                            :now 10 :ttl 10 :nonce "n"})))
    (is (thrown? clojure.lang.ExceptionInfo
                 (release/issue-deployment issuer-seed "rid-ci" final
                                           {:artifact-cid "other" :environment "prod"
                                            :previous-artifact-cid "bafy-previous"
                                            :revision "v2" :previous-revision "v1"
                                            :now 10 :ttl 10 :nonce "n"})))
    (is (thrown? clojure.lang.ExceptionInfo
                 (release/issue-deployment
                  issuer-seed "rid-ci"
                  (assoc-in final [:verdict :verdict/artifacts]
                            [{:path "ordinary.bin" :cid "bafy-artifact"}])
                  {:artifact-cid "bafy-artifact" :previous-artifact-cid "bafy-previous"
                   :environment "prod" :revision "v2" :previous-revision "v1"
                   :now 10 :ttl 10 :nonce "n"})))
    (let [issued (release/issue-deployment
                  issuer-seed "rid-ci" final
                  {:artifact-cid "bafy-artifact" :environment "prod"
                   :previous-artifact-cid "bafy-previous"
                   :revision "v2" :previous-revision "v1"
                   :now 10 :ttl 10 :nonce "n"})]
      (is (= verdict-cid (get-in issued [:capability :cd.capability/verdict-cid])))
      (is (capability/verify
           {:rid "rid-ci" :issuers #{(ed/did-key-from-seed issuer-seed)} :now 11
            :environment "prod" :artifact-cid "bafy-artifact" :verdict-cid verdict-cid
            :previous-artifact-cid "bafy-previous"
            :revision "v2" :previous-revision "v1"}
           issued)))))
